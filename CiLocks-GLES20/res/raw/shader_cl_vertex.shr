#version 100

uniform vec3 u_CenterLight; // in view space
uniform mat4 u_RingLight; // in view space
uniform mat4 u_MVPMatrix;
uniform mat4 u_MVMatrix;

attribute vec4 a_Position;
attribute vec2 a_UV;
attribute vec3 a_Normal;
attribute vec3 a_Tangent;
attribute vec3 a_Bitangent;

varying vec2 v_UV;
varying vec3 v_CenterLight;
varying float v_CenterDistance;
varying vec3 v_RingLight;
varying float v_RingDistance;


void main()
{
	gl_Position = u_MVPMatrix * a_Position;
	
	mat3 mv = mat3(u_MVMatrix);
	vec3 normal = mv * a_Normal; // normal vector should be normalized
	vec3 tangent = mv * a_Tangent; // tangent vector should be normalized
	vec3 bitangent = mv * a_Bitangent; // bitangent vector should be normalized
	
	// own transpose
	mat3 TBN = mat3
		(
			vec3(tangent.x, bitangent.x, normal.x),
			vec3(tangent.y, bitangent.y, normal.y),
			vec3(tangent.z, bitangent.z, normal.z)
		);
	
	// compute varyings
	v_UV = a_UV;
	
	// transfer light vectors to tangent space
	vec3 position = vec3(u_MVMatrix * a_Position);
	
	v_CenterLight = TBN * normalize(u_CenterLight - position);
	v_CenterDistance = distance(u_CenterLight, position);
	
	mat3 rl = mat3(u_RingLight);
	vec3 radial = position - rl[0];
	vec3 rlpos = rl[0] + length(rl[2]) * normalize(radial - dot(radial, rl[1]) * rl[1]);
	v_RingLight = TBN * normalize(rlpos - position);
	v_RingDistance = distance(rlpos, position);
}